/*
 * MIT License
 *
 * Copyright (C) 2021 Marcus Fihlon and the individual contributors to
 * Pandemieende Updater.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'com.github.spotbugs' version '4.7.5'
    id 'pmd'
    id 'checkstyle'
}

group 'ch.pandemieende.updater'
version '1.0-SNAPSHOT'

mainClassName = 'ch.pandemieende.updater.Main'
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
}

configurations {
    developmentOnly
}

dependencies {
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.4.1'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-classic:1.2.6'
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'com.opencsv:opencsv:5.5.2'
    testCompileOnly 'com.github.spotbugs:spotbugs-annotations:4.4.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testImplementation 'org.junit.platform:junit-platform-engine:1.7.2'
}

test.classpath += configurations.developmentOnly
run.classpath += configurations.developmentOnly

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

shadowJar {
    mergeServiceFiles()
    archiveBaseName.set('pandemieende-updater')
    archiveClassifier.set('')
    archiveVersion.set('')
}

spotbugs {
    toolVersion = '4.2.0'
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugsTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    configFile file("${projectDir}/config/checkstyle/checkstyle.xml")
    toolVersion "8.39"
}

pmd {
    toolVersion = "6.31.0"
    ruleSets = []
    ruleSetFiles = files("${projectDir}/config/pmd/pmd-rules.xml")
}

task createProperties {
    doLast {
        def version = project.version.toString()
        def file = new File("$buildDir/resources/main/version.txt")
        file.write(version)
    }
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}

assemble.dependsOn check
jar.dependsOn createProperties
